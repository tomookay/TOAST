<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_WriteParams" Id="{c40414b0-32f9-03ed-181a-4fea34577423}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WriteParams
VAR_INPUT
	iWrite			: BOOL := FALSE;(* Rising edge starts program execution *)
	iNetId			: T_AmsNetId := '';	(* TwinCAT system network address *)
	iFileName		: T_MaxString := '';(* CSV destination file path and name *)
END_VAR
VAR_OUTPUT
	qBusy			: BOOL;
	qError			: BOOL;
	qErrId			: UDINT;
	qStep			: DWORD;
END_VAR
VAR
	sCSVLine		: T_MaxString := '';(* Single CSV text line (row, record), we are using string as record buffer (your are able to see created fields) *)
	sCSVField		: T_MaxString := '';(* Single CSV field value (column, record field) *)
	fbFileOpen		: FB_FileOpen;(* Opens file *)
	fbFileClose		: FB_FileClose;(* Closes file *)
	fbFilePuts		: FB_FilePuts;(* Writes one record (line) *)
	fbWriter		: FB_CSVMemBufferWriter;(* Helper function block used to create CSV data bytes (single record line) *)
	nRow	 		: UDINT 	:= 0;(* Row number (record) *)
	nColumn			: UDINT 	:= 0;(* Column number (record field) *)
	hFile			: UINT		:= 0;(* File handle of the source file *)
	step			: DWORD 	:= 0;
END_VAR

VAR_IN_OUT
	iqdatabase		: ARRAY[0..constCSV.MAX_CSV_ROWS, 0..constCSV.MAX_CSV_COLUMNS ] OF STRING(constCSV.MAX_CSV_FIELD_LENGTH);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//based on sample code from Beckhoff 35419787.html

CASE step OF
	0:	(* Wait for rising edge at bWrite variable *)
		IF iWrite THEN
			//iWrite 	:= FALSE;
			qBusy 	:= TRUE;
			qError	:= FALSE;
			qErrId	:= 0;
			hFile	:= 0;
			nRow	:= 0;
			nColumn	:= 0;
			step 	:= 1;
		END_IF

	1:	(* Open source file *)
		fbFileOpen(  bExecute := FALSE  );
		fbFileOpen( sNetId := iNetId, sPathName := iFileName, nMode := FOPEN_MODEWRITE OR FOPEN_MODETEXT,(* Open file in TEXT mode! *)
					ePath := PATH_GENERIC, bExecute := TRUE );
		step := 2;

	2:(* Wait until open not busy *)
		fbFileOpen( bExecute := FALSE, bError => qError, nErrID => qErrId, hFile => hFile );
		IF NOT fbFileOpen.bBusy THEN
			IF NOT fbFileOpen.bError THEN
				step := 3;
			ELSE(* Error: file not found? *)
				step := 100;
			END_IF
		END_IF

	3:(* Convert one PLC record to CSV format *)
		sCSVLine := '';
		fbWriter.eCmd := eEnumCmd_First;(* Write first field value *)
		IF nRow <= constCSV.MAX_CSV_ROWS THEN

			FOR nColumn := 0 TO constCSV.MAX_CSV_COLUMNS BY 1 DO

				sCSVField := STRING_TO_CSVFIELD( iqdatabase[ nRow, nColumn ], FALSE );(* TODO: Get field value from your application *)

				(* Add new field to the record buffer *)
				fbWriter( 	pBuffer := ADR( sCSVLine ), cbBuffer := SIZEOF( sCSVLine ) - 1, putValue := sCSVField, pValue := 0, cbValue := 0,
							bCRLF := ( nColumn = constCSV.MAX_CSV_COLUMNS ) );(* bCRLF == TRUE => Write CRLF after the last field value *)
				IF fbWriter.bOk THEN
					fbWriter.eCmd := eEnumCmd_Next;(* Write next field value *)
				ELSE(* Error *)
					step := 100;
					RETURN;
				END_IF

			END_FOR(* FOR nColumn := 0... *)

			(* FB_FilePuts adds allready CR (carriage return) to the written line.
			We have to replace the $R$L characters with $L character to avoid double CR. *)
			IF RIGHT( sCSVLine, 2 ) = '$R$L' THEN
				sCSVLine := REPLACE( sCSVLine, '$L', 2, LEN( sCSVLine ) - 1 );
			END_IF

			nRow := nRow + 1;(* Increment number of created records (rows) *)
			step := 4;(* Write record to the file *)

		ELSE(* All rows written => Close file *)
			step := 10;
		END_IF

	4:	(* Write single text line *)
		fbFilePuts( bExecute := FALSE );
		fbFilePuts( sNetId := iNetId, hFile := hFile, sLine := sCSVLine, bExecute := TRUE );
		step := 5;

	5:(* Wait until write not busy *)
		fbFilePuts( bExecute := FALSE, bError => qError, nErrID => qErrId );
		IF NOT fbFilePuts.bBusy THEN
			IF NOT fbFilePuts.bError THEN
				step := 3;(* Write next record *)
			ELSE(* Error *)
				step := 100;
			END_IF
		END_IF

	10:	(* Close source file *)
		fbFileClose( bExecute := FALSE );
		fbFileClose( sNetId := iNetId, hFile := hFile, bExecute := TRUE );
		step := 11;

	11:(* Wait until close not busy *)
		fbFileClose( bExecute := FALSE, bError => qError, nErrID => qErrId );
		IF ( NOT fbFileClose.bBusy ) THEN
			hFile := 0;
			step := 100;
		END_IF

	100: (* Error or ready step => cleanup *)
		IF ( hFile <> 0 ) THEN
			step := 10; (* Close the source file *)
		ELSE
			qBusy := FALSE;
			step := 0;	(* Ready *)
		END_IF

END_CASE


//move to outside world
qStep := step;
]]></ST>
    </Implementation>
    <LineIds Name="FB_WriteParams">
      <LineId Id="996" Count="104" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>