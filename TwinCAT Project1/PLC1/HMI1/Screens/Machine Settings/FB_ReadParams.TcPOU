<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_ReadParams" Id="{7c1ef037-fcf9-07ca-054e-5ead071fbc08}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ReadParams
VAR_INPUT
	iRead			: BOOL := FALSE;(* Rising edge starts program execution *)
	iNetId			: T_AmsNetId := '';	(* TwinCAT system network address *)
	iFileName		: T_MaxString := 'C:\PLC\machinesettings.csv';(* CSV source file path and name *)

END_VAR
VAR_OUTPUT
	qBusy			: BOOL;
	qError			: BOOL;
	qErrId			: UDINT;
	qStep			:DWORD;
END_VAR
VAR
	nRow	 		: UDINT 	:= 0;(* Row number (record) *)
	nColumn			: UDINT 	:= 0;(* Column number (record field) *)
	hFile			: UINT		:= 0;(* File handle of the source file *)
	step			: DWORD 	:= 0;
	fbFileOpen		: FB_FileOpen;(* Opens file *)
	fbFileClose		: FB_FileClose;(* Closes file *)
	fbFileGets		: FB_FileGets;(* Reads one record (line) *)
	fbReader		: FB_CSVMemBufferReader;(* Helper function block used to parse CSV data bytes (single record line) *)
	sCSVLine		: T_MaxString := '';(* Single CSV text line (row, record), we are using string as record buffer (your are able to see read fields)  *)
	sCSVField		: T_MaxString := '';(* Single CSV field value (column, record field) *)


END_VAR

VAR_IN_OUT
		iqdatabase		: ARRAY[0..constCSV.MAX_CSV_ROWS, 0..constCSV.MAX_CSV_COLUMNS ] OF STRING(constCSV.MAX_CSV_FIELD_LENGTH);(* Target PLC database *)
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[//based on sample code from Beckhoff 35419787.html

CASE step OF
	0:	(* Wait for rising edge at bRead variable *)
		IF iRead THEN
		//	bRead 	:= FALSE;
			qBusy 	:= TRUE;
			qError	:= FALSE;
			qErrId	:= 0;
			hFile	:= 0;
			nRow	:= 0;
			nColumn	:= 0;
			MEMSET( ADR( iqdatabase ), 0, SIZEOF( iqdatabase ) );
			step 		:= 1;
		END_IF

	1:	(* Open source file *)
		fbFileOpen(  bExecute := FALSE  );
		fbFileOpen( sNetId := iNetId, sPathName := iFileName, nMode := FOPEN_MODEREAD OR FOPEN_MODETEXT,(* Open file in TEXT mode! *)
					ePath := PATH_GENERIC, bExecute := TRUE );
		step := 2;

	2:(* Wait until open not busy *)
		fbFileOpen( bExecute := FALSE, bError => qError, nErrID => qErrID, hFile => hFile );
		IF NOT fbFileOpen.bBusy THEN
			IF NOT fbFileOpen.bError THEN
				step := 3;
			ELSE(* Error: file not found? *)
				step := 100;
			END_IF
		END_IF

	3:	(* Read single line (record) *)
		fbFileGets( bExecute := FALSE );
		fbFileGets( sNetId := iNetId, hFile := hFile, bExecute := TRUE );
		step := 4;

	4:(* Wait until read not busy *)
		fbFileGets( bExecute := FALSE, bError => qError, nErrID => qErrID, sLine => sCSVLine );
		IF NOT fbFileGets.bBusy THEN
			IF NOT fbFileGets.bError THEN
				IF fbFileGets.bEOF THEN
					step := 10;(* End of file reached => Close source file *)
				ELSE

					(* FB_FileGets returns text line without the CR (carriage return) character.
					We have to restore the CR character (replace the $L character with $R$L characters) *)
					IF RIGHT( sCSVLine, 1 ) = '$L' THEN
						sCSVLine := REPLACE( sCSVLine, '$R$L', 2, LEN( sCSVLine ) );
					END_IF

					step := 5;
				END_IF
			ELSE(* Error *)
				step := 100;
			END_IF
		END_IF

	5:(* Parse single line (record) *)
		fbReader.eCmd := eEnumCmd_First;(* Read first field value *)
		REPEAT
			fbReader( pBuffer := ADR( sCSVLine ), cbBuffer := SIZEOF( sCSVLine ), getValue => sCSVField );
			IF fbReader.bOk THEN
				fbReader.eCmd := eEnumCmd_Next;(* Read next field value *)

				IF ( nRow <= constCSV.MAX_CSV_ROWS ) THEN
					IF ( nColumn <= constCSV.MAX_CSV_COLUMNS ) THEN
						iqdatabase[nRow, nColumn] := CSVFIELD_TO_STRING( sCSVField, FALSE );(* TODO: Save or use the field value in your application *)
					END_IF
				END_IF

				nColumn := nColumn + 1;(* Increment number of read columns *)
				IF fbReader.bCRLF THEN(* CRLF == TRUE => End of reacord reached *)
					nRow 		:= nRow + 1;(* Increment number of read records *)
					nColumn 	:= 0;(* Reset number of columns *)
				END_IF

			ELSE(* Error: End of record reached or all fields read *)
				step := 3;(* Try to read next line *)
			END_IF
		UNTIL NOT fbReader.bOk
		END_REPEAT

	10:	(* Close source file *)
		fbFileClose( bExecute := FALSE );
		fbFileClose( sNetId := iNetId, hFile := hFile, bExecute := TRUE );
		step := 11;

	11:(* Wait until close not busy *)
		fbFileClose( bExecute := FALSE, bError => qError, nErrID => qErrID );
		IF ( NOT fbFileClose.bBusy ) THEN
			hFile := 0;
			step := 100;
		END_IF

	100: (* Error or ready step => cleanup *)
		IF ( hFile <> 0 ) THEN
			step := 10; (* Close the source file *)
		ELSE
			qBusy := FALSE;
			step := 0;	(* Ready *)
		END_IF

END_CASE


//move to outside world
qStep := step;]]></ST>
    </Implementation>
    <LineIds Name="FB_ReadParams">
      <LineId Id="10" Count="1" />
      <LineId Id="14" Count="102" />
      <LineId Id="151" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>